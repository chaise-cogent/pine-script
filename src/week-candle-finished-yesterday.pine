// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © chaise_cogent
//
// Script version = 4.0.1-fix-wrong-candles
// The script slides start/end dates of 1-week candles forward,
// as if Yesterday the previous Week-candle closed.
// For example, if today is Wednesday —- then virtual weeks
// open on Saturdays and close on Tuesdays.

//@version=6
indicator('Week candle closed yesterday', overlay=false)

// TODO: check candles at time from 12:00am until time of your timezone -- for positive timezones

show_on_real = input.bool(false, 'Show virtual levels on real 1W candles?')
bull_color = input.color(color.new(color.green, 20), title = 'Bullish Candle')
bear_color = input.color(color.new(color.red, 20), title = 'Bearish Candle')
is_one_week_timeframe = timeframe.isweekly and timeframe.multiplier == 1

get_date_human_readable(simple int days_back = 0) =>
    // Returns date in ISO-format specified days ago: '2025-12-31'
    _today_midnight = timestamp(
      year(timenow), month(timenow), dayofmonth(timenow), 0, 0, 0)
    _yesterday_midnight = _today_midnight - days_back * 24 * 3600 * 1000
    str.format_time(_yesterday_midnight, 'yyyy-MM-dd', syminfo.timezone)

last_week_custom_open = request.security(syminfo.tickerid, '1D', open[7])
last_week_custom_high = request.security(syminfo.tickerid, '1D', ta.highest(high[1], 7))
last_week_custom_low = request.security(syminfo.tickerid, '1D', ta.lowest(low[1], 7))
last_week_custom_close = request.security(syminfo.tickerid, '1D', close[1])

var int previous_weeks_daily_index =
  dayofweek(timenow) == dayofweek.sunday or
  dayofweek(timenow) == dayofweek.monday or
  false ?
  1 :
  9 - dayofweek(timenow) // Tue(3) => index=6, Wed(4) => index=5, ..., Sat(7) => index=2

before_last_week_custom_open = request.security(syminfo.tickerid, '1D', open[previous_weeks_daily_index + 6])
before_last_week_custom_high = request.security(syminfo.tickerid, '1D', ta.highest(high[previous_weeks_daily_index], 7))
before_last_week_custom_low = request.security(syminfo.tickerid, '1D', ta.lowest(low[previous_weeks_daily_index], 7))
before_last_week_custom_close = request.security(syminfo.tickerid, '1D', close[previous_weeks_daily_index])

series float custom_open = na
series float custom_close = na
series float custom_high = na
series float custom_low = na

if is_one_week_timeframe
    if barstate.islast
        custom_open := last_week_custom_open
        custom_high := last_week_custom_high
        custom_low := last_week_custom_low
        custom_close := last_week_custom_close
    else
        custom_open := before_last_week_custom_open
        custom_high := before_last_week_custom_high
        custom_low := before_last_week_custom_low
        custom_close := before_last_week_custom_close
else
    custom_open := open
    custom_high := high
    custom_low := low
    custom_close := close

candle_color = custom_open <= custom_close ? bull_color : bear_color

// Main plotting of the candles
plotcandle(custom_open, custom_high, custom_low, custom_close,
  color = candle_color, wickcolor = candle_color, bordercolor = candle_color,
  title = 'OHLC', force_overlay = false)

// Shows start/end dates of the previous closed week-candle
if barstate.islast
    table_settings = table.new(position.bottom_right, columns = 2, rows = 3,
      border_color = color.white, bgcolor = color.new(color.black, 20),
      border_width = 1, force_overlay = false)

    table.clear(table_settings, start_column = 0, start_row = 0,
      end_column = 1, end_row = 2)
    table.cell(table_settings, 0, 0, 'Last (virtual) week',
      text_color = color.white, text_formatting = text.format_bold)
    table.merge_cells(table_settings, start_column = 0, start_row = 0,
      end_column = 1, end_row = 0)
    table.cell(table_settings, 0, 1, 'Started', text_formatting = text.format_bold,
      text_color = color.white)
    table.cell(table_settings, 1, 1, 'Last day', text_formatting = text.format_bold,
      text_color = color.white)
    table.cell(table_settings, 0, 2, get_date_human_readable(7),
      text_color = color.white)
    table.cell(table_settings, 1, 2, get_date_human_readable(1),
      text_color = color.white)

// Next lines are only supplementing 
// Plotting virtual candle's levels on real candles
week_open_conditional = show_on_real and is_one_week_timeframe ? custom_open : na
week_high_conditional = show_on_real and is_one_week_timeframe ? custom_high : na
week_low_conditional = show_on_real and is_one_week_timeframe ? custom_low : na
week_close_conditional = show_on_real and is_one_week_timeframe ? custom_close : na

plot(week_open_conditional, style=plot.style_circles, color = color.new(bull_color, 0),
  linewidth = 2, force_overlay = true, title = 'virt open')
plot(week_high_conditional, style=plot.style_circles, color = color.gray,
  linewidth = 2, force_overlay = true, title = 'virt high')
plot(week_low_conditional, style=plot.style_circles, color = color.gray,
  linewidth = 2, force_overlay = true, title = 'virt low')
plot(week_close_conditional, style=plot.style_circles, color = color.new(bear_color, 0),
  linewidth = 2, force_overlay = true, title = 'virt close')
